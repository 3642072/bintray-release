apply plugin: 'com.novoda.bintray-release'
apply plugin: 'groovy'
apply plugin: 'maven'

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
        classpath 'com.novoda:bintray-release:0.6.0'
    }
}

repositories {
    google()
    jcenter()
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
    testRuntime files(createClasspathManifest)
}

compileGroovy {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
}

publish {
    userOrg = 'novoda'
    groupId = 'com.novoda'
    artifactId = rootProject.name
    version = project.version
    description = 'Super duper easy way to release your Android and other artifacts to bintray'
    website = "https://github.com/novoda/${rootProject.name}"
}
